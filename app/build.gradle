apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}
loadProperties("config.properties")

def loadProperties(String sourceFileName) {
    def config = new Properties()
    def propFile = new File(sourceFileName)
    if (propFile.canRead()) {
        System.out.println("Loading property file: " + propFile.absolutePath)
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            System.out.println("setting " + property.key)
            project.ext[property.key] = property.value;
        }
        return true;
    } else {
        System.err.println("Couldn't load property file: " + propFile.absolutePath)
        return false;
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.indri.musictrivia"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resValue "string", "CLIENT_ID", project.ext.get("CLIENT_ID")
        resValue "string", "REDIRECT_URI", project.ext.get("REDIRECT_URI")
        resValue "string", "REDIRECT_SCHEME", project.ext.get("REDIRECT_SCHEME")
        resValue "string", "REDIRECT_HOST", project.ext.get("REDIRECT_HOST")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

kapt {
    arguments {
        arg("androidManifestFile", variant.outputs[0].processResourcesTask.manifestFile)
    }
    generateStubs = true
}

retrolambda {
    jvmArgs '-noverify'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Android support libraries
    compile 'com.android.support:support-v4:' + rootProject.supportLibVersion
    compile 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion
    compile 'com.android.support:design:' + rootProject.supportLibVersion
    compile 'com.android.support:cardview-v7:' + rootProject.supportLibVersion
    compile 'com.android.support:gridlayout-v7:' + rootProject.supportLibVersion
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Network
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'

    //Rx libraries
    compile 'io.reactivex.rxjava2:rxjava:2.1.1'
    compile 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.artemzin.rxjava:proguard-rules:1.3.0.0'
    compile 'com.github.pwittchen:reactivenetwork:0.9.1'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'

    //DI libraries
    compile 'com.google.dagger:dagger:2.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.2'
    kapt 'com.google.dagger:dagger-compiler:2.2'
    provided 'javax.annotation:jsr250-api:1.0'

    // Spotify authentication and authorization
    compile 'com.spotify.sdk:spotify-auth:1.0.0-beta11@aar'

    // Spotify music playback
    compile 'com.spotify.sdk:spotify-player:1.0.0-beta11@aar'

    //AndroidAnnotations
    annotationProcessor 'org.androidannotations:androidannotations:4.2.0'
    compile 'org.androidannotations:androidannotations-api:4.2.0'
    kapt 'org.androidannotations:androidannotations:4.2.0'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Anko Commons
    compile "org.jetbrains.anko:anko-commons:$anko_version"

    //Utilities
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.squareup.picasso:picasso:2.5.2'


    testCompile 'junit:junit:4.12'

}
